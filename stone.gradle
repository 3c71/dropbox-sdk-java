apply plugin: 'java'

task generateStoneSources(type: Exec) {
    description 'Runs Stone generation script to generate API spec Java source files.'

    ext {
        generateScriptFile = file('run-stone-codegen')
        generatorDir = file('generator')
        specDir = file(project.properties.get('com.dropbox.api.specDir','spec'))
        outputDir = file("${generatedSources}/stone")
        logDir = file("${buildDir}")
        logFile = file("${logDir}/stone.log")
    }

    logDir.mkdirs()
    outputDir.mkdirs()

    if (!specDir.exists()) {
        throw new GradleException("Dropbox API Stone spec directory does not exist: ${specDir.absolutePath}")
    }

    inputs.file generateScriptFile
    inputs.dir fileTree(dir: generatorDir, exclude: '**/*.pyc')
    inputs.sourceDir fileTree(dir: specDir, include: '**/*.stone')
    outputs.dir outputDir

    standardOutput = new FileOutputStream(logFile)

    commandLine generateScriptFile.absolutePath, specDir.absolutePath, outputDir.absolutePath
}

sourceSets {
    main {
        java {
            srcDir "${generatedSources}"
        }
    }
}

compileJava {
    dependsOn generateStoneSources
}
